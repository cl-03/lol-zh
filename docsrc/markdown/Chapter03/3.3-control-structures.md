# 3.3 Control Structures

---

Author: Doug Hoyte <[doug@hoytech.com](mailto:doug@hoytech.com)>

Translator: Yuqi Liu <[yuqi.lyle@outlook.com](mailto:yuqi.lyle@outlook.com)>

---

Although this book is focused on COMMON LISP, it is written for and about the Scheme programming language as well. Scheme is a wonderful language that, although lacking many features lisp programmers take for granted, still offers a flexible enough core for the professional lisp programmer to extend as necessary[2](https://letoverlambda.com/index.cl/guest/chap3.html#). Similarly, there are a few features Scheme programmers rely on heavily that COMMON LISP doesn't specifically address. But comparisons between the features offered by each language are, with a few exceptions, meaningless. The gaps between the two languages can be, and frequently are, bridged. The bridges we use to cross between the two languages are, of course, macros.


Scheme's **let** form is in one respect more powerful than its COMMON LISP counterpart. Scheme's **let** form supports something called a _named let_. In Scheme, you can optionally insert a symbol before the bind list of a **let** form and Scheme will bind a function named by the provided symbol around the let body[3](https://letoverlambda.com/index.cl/guest/chap3.html#). This function accepts new arguments for the values provided in the **let** bindings, providing a very convenient way to express loops.
```
(defmacro nlet (n letargs &rest body)
  `(labels ((,n ,(mapcar #'car letargs)
              ,@body))
     (,n ,@(mapcar #'cadr letargs))))
```
Luckily we can build a bridge between Scheme and COMMON LISP with the **nlet** macro. **Nlet** lets us code in a Scheme style by emulating Scheme's named lets. In **nlet-fact**, **nlet** is used to define the factorial function by using a named let:
```
(defun nlet-fact (n)
  (nlet fact ((n n))
    (if (zerop n)
      1
      (* n (fact (- n 1))))))
```
Because **nlet** is one of our first macros, let's slow down and analyse it in depth. Sometimes to understand a macro it helps to _macroexpand_ an example use of this macro[4](https://letoverlambda.com/index.cl/guest/chap3.html#). To do that, provide a list representing this macro invocation to the **macroexpand** function. Notice that **macroexpand** will only expand macros that have their symbols in the first element of the list and will not expand nested macro invocations for you[5](https://letoverlambda.com/index.cl/guest/chap3.html#). In the following, we've copied an invocation of **nlet** directly from **nlet-fact**, quoted it, and passed it to **macroexpand**:
```
* (macroexpand
    '(nlet fact ((n n))
       (if (zerop n)
         1
         (* n (fact (- n 1))))))

(LABELS ((FACT (N)
           (IF (ZEROP N)
             1
             (* N (FACT (- N 1))))))
  (FACT N))
T
```
The expansion uses the **labels** special form to bind a function around the provided body. The function is named according to the symbol used in the named let form. It takes as arguments the values bound with **nlet**, here only **n**. Since this function can be recursive, **nlet** implements a useful iteration construct.


Although simple macros might just be filling out backquote templates, most complicated macros at minimum make use of lisp's extensive list processing functions. **Mapcar**, applying a function to every element in a list and returning a list of the resulting values, turns up especially often in macros. Tellingly, **mapcar** turns up often in regular lisp code as well. Lisp has been tuned to be as useful as possible for processing lists. In all kinds of lisp programming, including macro construction, we splice, merge, reduce, map, and filter lists. The only difference is that when programming macros, the output subsequently gets passed to a compiler or interpreter. Programming macros in lisp is actually the same process as programming regular lisp.


But what does it mean to say that **nlet** is a new control structure? A control structure is just a fancy way of describing some construct that doesn't follow the behaviour of a function. A function will evaluate each argument from left to right, bind the results in an environment, and execute machine code specified by some **lambda** form. Since **nlet** doesn't evaluate its arguments directly, instead splicing them into some chunk of lisp code, we have changed the flow of evaluation for **nlet** forms and thus have created a new control structure.


By this broad definition, virtually all macros—at least all interesting macros—define new control structures. When people say "only use macros when functions won't do", they mean that for any definition where you don't want to evaluate certain arguments, or you want to evaluate them out of order, or more than once, you will need to use a macro. Functions, no matter how cleverly written, simply will not work.


The **nlet** macro demonstrates one way that COMMON LISP was designed for macro writers. In binding forms such as **let**, it is a common convention for a variable to be bound to nil if no value is specified along with the variable name. In other words, **(let ((a)) a)** will be nil[6](https://letoverlambda.com/index.cl/guest/chap3.html#). In Scheme, a language slightly less macro-writer friendly, this case must be checked for as a special case when iterating through such bindings because **(car nil)** and **(cdr nil)** raise type errors. In COMMON LISP, **(car nil)**, **(cdr nil)**, and therefore **(car (cdr nil))** and **(cadr nil)** are defined to return **nil**, allowing the second **mapcar** in **nlet** to work even if the empty let variable convention is used. This COMMON LISP feature is from Interlisp[INTERLISP].


Our **nlet** macro is different from Scheme's named lets in one subtle way. In this case, the interface to the macro is acceptable but the expansion may not be. As is common when programming across multiple levels, our mental model of the code can easily be slightly different from reality. In Scheme, a tail call of a named let is guaranteed to take up no additional stack space since Scheme is required, by the standard, to make this specific optimisation. This is not the case in COMMON LISP, however, so it is possible for stack overflows to occur in our COMMON LISP version of **nlet** that would not happen with named lets in Scheme. In [section 5.4, Code-Walking with Macrolet](https://letoverlambda.com/index.cl/guest/chap5.html#sec_4) we will see how to write a version of **nlet** with an identical interface but a potentially more efficient expansion[7](https://letoverlambda.com/index.cl/guest/chap3.html#).
