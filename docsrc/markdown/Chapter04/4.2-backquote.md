# 4.2 Backquote

---

Author: Doug Hoyte

Translator: Yuqi Liu <[yuqi.lyle@outlook.com](mailto:yuqi.lyle@outlook.com)>

---

_Backquote_, sometimes known as _quasiquote_[2](https://letoverlambda.com/index.cl/guest/chap4.html#), and displayed as `, is a relative new-comer to mainstream lisp programming, and the concept is still almost completely foreign to languages other than lisp.


Backquote has a bizarre history of development in parallel with lisp. It is reported[QUASIQUOTATION] that early on nobody believed that nested backquotes worked right until a sharp programmer realised that they actually did work right—people's ideas of what was right were wrong. The nested backquote is notoriously difficult to understand. Even Steele, the father of COMMON LISP, complains about it[CLTL2-P530].


In principle, lisp doesn't need backquote. Anything that can be done with backquote can be done with other list building functions. However, backquote is so useful for macro programming, which in lisp means all programming, that lisp professionals have come to rely on it heavily.


First off, we need to understand regular quotation. In lisp, when we prefix a form with the quote character (') we are informing lisp that the following form should be treated as raw data, and not code to be evaluated. Rather, quote reads in as code that, when evaluated, returns a form. We sometimes say that quote _stops_ or _turns off_ the evaluation of a form.


Backquote can be used as a substitute for quote in lisp. Unless certain special characters, called _unquote_ characters, appear in a form, backquote stops evaluation in the same way as quote. As the name suggests, these unquote characters reverse the evaluation semantics. We sometimes say that an unquote _restarts_ or _turns back on_ the evaluation of a form.


There are three main types of unquote: regular unquote, splicing unquote, and destructive splicing unquote.


To perform a regular unquote, we use the comma operator:
```
* (let ((s 'hello))
    `(,s world))

(HELLO WORLD)
```
Although the expression we are unquoting is simply a symbol to evaluate, **s**, it can instead be any lisp expression that evaluates to something meaningful for whatever context it appears in the backquote template. Whatever the results are, they are inserted into the resulting list in the car position of where they appear in the backquote template.


In lisp form notation, we can use . to indicate that we want to explicitly put something in the cdr of the list structure we are creating. If we put a list there, the resulting form from the backquote will remain a valid list. But if we put something else there, we will get a new, non-list structure.


We have this ability inside of backquote like everywhere else[3](https://letoverlambda.com/index.cl/guest/chap4.html#). Thanks to the design of backquote, we can even unquote things in this position:
```
* (let ((s '(b c d)))
    `(a . ,s))

(A B C D)
```
Inserting lists into the cdr position of a list being created from a backquote template is so common that backquote takes it a step further with splicing unquote. The ., combination above is useful, but is incapable of inserting elements into the middle of a list. For that, we have the splicing unquote operator:
```
* (let ((s '(b c d)))
    `(a ,@s e))

(A B C D E)
```
Neither ., nor ,@ modify the list being spliced in. For instance, after evaluating the backquote in both of the previous forms, **s** will still be bound to the three element list **(B C D)**. Although it is not strictly required to by the standard, the **(B C D)** in the **(A B C D)** form above is allowed to share structure with the spliced-in list, **s**. However, in the list **(A B C D E)**, this list structure is guaranteed to be freshly allocated when the backquote is evaluated since ,@ is forbidden to modify the lists being spliced in. Splicing unquote is non-destructive because generally we want to think about backquote as being a re-usable template for creating lists. Destructively modifying the list structure of data that isn't freshly allocated on every evaluation of the backquote code can have undesirable effects upon future expansions.


However, COMMON LISP also provides a destructive version of splicing unquote which can be used anywhere splicing unquote can. To splice destructively, use ,. instead. Destructive splicing works the same as regular splicing except that the list being spliced in may be modified during the evaluation of the backquote template. As well as being only one character different from regular splicing, this notation is a clever re-use of the . character from the ., cdr position unquoting we looked at above.
To see this in action, here we destructively modify the list pointed to by to-splice:
```
* (defvar to-splice '(B C D))

TO-SPLICE
* `(A ,.to-splice E)

(A B C D E)
* to-splice

(B C D E)
```
Destructively modifying lists to be spliced in can be dangerous. Consider the following use of destructive splicing:
```
(defun dangerous-use-of-bq ()
  `(a ,.'(b c d) e))
```
The first time **dangerous-use-of-bq** is called, the expected answer is returned: **(A B C D E)**. But since it uses destructive splicing and modifies a list that isn't freshly generated—the quoted list—we can expect various undesirable consequences. In this case, the second time **dangerous-use-of-bq** is evaluated, the **(B C D)** form is now really a **(B C D E)** form, and when backquote tries to destructively splice this list onto the remainder of the backquote template, **(E)**—its own tail—it creates a list containing a _cycle_. We discuss cycles in more detail in [section 4.5, Cyclic Expressions](https://letoverlambda.com/index.cl/guest/chap4.html#sec_5).


However, there are many cases where destructive splicing is perfectly safe. Don't let **dangerous-use-of-bq** scare you if you need more efficiency in your backquote forms. There are many operations that create fresh list structure that you are probably planning on throwing out anyways. For instance, splicing the results of a mapcar is so common and safe that the following could probably become a programming idiom:
```
(defun safer-use-of-bq ()
  `(a
    ,.(mapcar #'identity '(b c d))
    e))
```
But there is a reason it hasn't. The most common use of backquote is for authoring macros, the part of lisp programming where speed matters least and where clarity matters most. If thinking about the _side-effects_ of your splicing operations distracts you even for a split second while creating and interpreting macros, it is probably not worth the trouble. This book sticks with regular splicing. The most common use of backquote is in macro construction but this is not its only use. Backquote is actually a useful domain specific language for the domain of mashing together lists, one made even more useful given the possibility of destructive splicing.


How does backquote work? Backquote is a read macro. Backquoted forms read in as code that, when evaluated, becomes the desired list. Going back to the example of the previous section on read-time evaluation, we can turn off _pretty printing_, quote the value of the backquote form, and print it out to see exactly how backquote forms read[4](https://letoverlambda.com/index.cl/guest/chap4.html#):
```
* (let (*print-pretty*) ; bind to nil
    (print
      '`(football-game
          (game-started-at
            ,(get-internal-real-time))
          (coin-flip
            ,(if (zerop (random 2))
               'heads
               'tails))))
    t)

(LISP::BACKQ-LIST
  (QUOTE FOOTBALL-GAME)
  (LISP::BACKQ-LIST
    (QUOTE GAME-STARTED-AT)
    (GET-INTERNAL-REAL-TIME))
  (LISP::BACKQ-LIST
    (QUOTE COIN-FLIP)
    (IF (ZEROP (RANDOM 2))
      (QUOTE HEADS)
      (QUOTE TAILS))))
T
```
In the above _ugly printed_ form, the function **LISP::BACKQ-LIST** is identical to list, except for its pretty printing behaviour. Notice that the comma operators are gone. COMMON LISP is fairly liberal in what it allows backquote to read in as, particularly for operations where shared structure is permitted.


Backquote also provides many interesting solutions to the amusing _non-problem_ of writing a lisp expression that evaluates to itself. These expressions are commonly called _quines_ after Willard Quine who studied them extensively and who, in fact, coined the term quasiquote—an alternative name for backquote[FOUNDATIONS-P31-FOOTNOTE3]. Here is a fun example of a quine that is attributed to Mike McMahon in [QUASIQUOTATION]:
```
* (let ((let '`(let ((let ',let))
                 ,let)))
    `(let ((let ',let)) ,let))

(LET ((LET '`(LET ((LET ',LET))
               ,LET)))
  `(LET ((LET ',LET)) ,LET))
```
To save you the _mental code-walk_:
```
* (equal * +)

T
```
Exercise: In the following evaluation, why is the backquote expanded into a regular quote? Isn't it quoted?
```
* '`q
'Q
```
