# 2.5 Let Over Lambda

---

Author: Doug Hoyte

Translator: Yuqi Liu <[yuqi.lyle@outlook.com](mailto:yuqi.lyle@outlook.com)>

---

_Let over lambda_ is a nickname given to a lexical closure. Let over lambda more closely mirrors the lisp code used to create closures than does most terminology. In a let over lambda scenario, the last form returned by a **let** statement is a **lambda** expression. It literally looks like **let** is sitting on top of **lambda**:
```
* (let ((x 0))
    (lambda () x))

#<Interpreted Function>
```
Recall that the **let** form returns the result of evaluating the last form inside its body, which is why evaluating this let over lambda form produced a function. However, there is something special about the last form in the **let**. It is a **lambda** form with **x** as a _free variable_. Lisp was smart enough to determine what **x** should refer to for this function: the **x** from the surrounding lexical environment created by the **let** form. And, because in lisp everything is of indefinite extent by default, the environment will be available for this function to use as long as it needs it.


So lexical scope is a tool for specifying exactly where references to a variable are valid, and exactly what the references refer to. A simple example of a closure is a _counter_, a closure that stores an integer in an environment and increments and returns this value upon every invocation. Here is how it is typically implemented, with a let over lambda:
```
(let ((counter 0))
  (lambda () (incf counter)))
```
This closure will return 1 the first time it is called, 2 the subsequent time, and so on. One way of thinking about closures is that they are functions with _state_. These functions are not mathematical functions, but rather procedures, each with a little memory of its own. Sometimes data structures that bundle together code and data are called _objects_. An object is a collection of procedures and some associated state. Since objects are so closely related to closures, they can often be thought of as one and the same. A closure is like an object that has exactly one method: **funcall**. An object is like a closure that you can **funcall** in multiple ways.


Although closures are always a single function and its enclosing environment, the multiple methods, inner classes, and static variables of object systems all have their closure counterparts. One possible way to simulate multiple methods is to simply return multiple **lambda**s from inside the same lexical scope:
```
(let ((counter 0))
  (values
    (lambda () (incf counter))
    (lambda () (decf counter))))
```
This _let over two lambdas_ pattern will return two functions, both of which access the same enclosing counter variable. The first increments it and the second decrements it. There are many other ways to accomplish this. One of which, **dlambda**, is discussed in [section 5.7, Dlambda](https://letoverlambda.com/index.cl/guest/chap5.html#sec_7). For reasons that will be explained as we go along, the code in this book will structure all data using closures instead of objects. Hint: It has to do with macros.
